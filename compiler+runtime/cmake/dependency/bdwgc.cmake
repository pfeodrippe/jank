set(CMAKE_C_FLAGS_OLD "${CMAKE_C_FLAGS}")
set(CMAKE_CXX_FLAGS_OLD "${CMAKE_CXX_FLAGS}")
set(BUILD_SHARED_LIBS_OLD ${BUILD_SHARED_LIBS})
set(CMAKE_CXX_CLANG_TIDY_OLD ${CMAKE_CXX_CLANG_TIDY})
  set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -w")
  set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -w")

  # Add -fPIC for WASM builds to support MAIN_MODULE dynamic linking
  if(jank_target_wasm)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -fPIC")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
  endif()

  if(NOT APPLE)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DREDIRECT_MALLOC=GC_malloc_uncollectable -DREDIR_MALLOC_AND_LINUX_THREADS")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DREDIRECT_MALLOC=GC_malloc_uncollectable -DREDIR_MALLOC_AND_LINUX_THREADS")
  endif()

  set(BUILD_SHARED_LIBS OFF)
  set(CMAKE_CXX_CLANG_TIDY "")
  set(enable_cplusplus ON CACHE BOOL "Enable C++")
  set(build_cord OFF CACHE BOOL "Build cord")
  set(enable_docs OFF CACHE BOOL "Enable docs")
  if(jank_target_wasm)
    # Disable threads for WASM - emscripten doesn't support bdwgc's threading model
    set(enable_threads OFF CACHE BOOL "Enable multi-threading support")
  else()
    set(enable_threads ON CACHE BOOL "Enable multi-threading support")
  endif()
  set(enable_large_config ON CACHE BOOL "Optimize for large heap or root set")
  # Enable thread-local allocation buffers (TLABs) for fast bump-pointer allocation
  set(enable_thread_local_alloc ON CACHE BOOL "Enable thread-local allocation optimization")
  # Enable parallel marking for faster GC
  set(enable_parallel_mark ON CACHE BOOL "Parallelize marking and free list construction")
  if(jank_target_wasm)
    # Skip gctba for wasm to avoid C++ header issues with emscripten
    set(enable_throw_bad_alloc_library OFF CACHE BOOL "Enable C++ gctba library build")
  else()
    set(enable_throw_bad_alloc_library ON CACHE BOOL "Enable C++ gctba library build")
  endif()
  set(enable_gc_debug OFF CACHE BOOL "Support for pointer back-tracing")

  if(jank_debug_gc)
    set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -DGC_ASSERTIONS")
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -DGC_ASSERTIONS")
    set(enable_gc_debug ON CACHE BOOL "Support for pointer back-tracing")
  endif()

  add_subdirectory(third-party/bdwgc EXCLUDE_FROM_ALL)

  unset(enable_cplusplus)
  unset(build_cord)
  unset(enable_docs)
  unset(enable_threads)
  unset(enable_large_config)
  unset(enable_thread_local_alloc)
  unset(enable_parallel_mark)
  unset(enable_throw_bad_alloc_library)
  unset(enable_gc_debug)
set(CMAKE_C_FLAGS "${CMAKE_C_FLAGS_OLD}")
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_OLD}")
set(BUILD_SHARED_LIBS ${BUILD_SHARED_LIBS_OLD})
set(CMAKE_CXX_CLANG_TIDY ${CMAKE_CXX_CLANG_TIDY_OLD})
